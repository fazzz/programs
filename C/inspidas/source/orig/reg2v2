C PROGRAM TO
C 1)CALCULATE DISTANCE CONSTRAINTS AND PUT THEM IN
C   LOWER DATA AND UPPER.DATA
C 2)CALCULATE DIHEDRAL DIRECTLY FROM X-RAY COORDINATES.
C                      WERNER BRAUN     9/'83
C                  ED. HARUO ABE      2/8/'84
C                  MD. YASUNOBU SENO    1/'85
C *BY THIS MODIFICATION OMEGA AND SIDECHAIN ANGLES CAN BE CALUCULATED.
C                  MD. T.NOGUTI        12/'85
C *FORMAT OF OUTPUT DIHEDRAL ANGLES IS CHANGED TO 10F8.3
C                  MD. K.MORIKAMI      11/'86
C *CONSTRAINTS TYPE3 CHANGE
C *ADD AND DELETE COMMENTS
C *DELETE UNUSED ROUTINES
C *MODIFY TO READ PROGRAM EASILY
C                  MD. K.MORIKAMI      12/'86
C *ADD TYPE4 CONSTRAINTS
C
C     INPUT FILE
C          20  :  ATOM COORDINATE AND CONNECTIVITY FILE (ABE FORMAT)
C          24  :  SIDE CHAIN DATA (OVREG2.DATA)
C     OUTPUT FILE
C          23  :  DIHEDRAL DATA (ECEPP FORMAT)
C          51  :  LOWER AND UPPER CONSTRAINTS DATA (NO FORMAT)
C          21  :  LOWER CONSTRAINTS DATA (WITH FORMAT)  *USUALLY DUMMY
C          22  :  UPPER CONSTRAINTS DATA (WITH FORMAT)  *USUALLY DUMMY
C
      COMMON /LISTRS/ NPREA,LIST(500)
      COMMON /SCHDAT/ NHEAVY(21),NCHI(21),NCHANG(4,7,21)
      COMMON /COORD/  CO(3,5000)
      DIMENSION NPAIR(20,2),NUM(5000),NUMRE(5000),
     1        OCUP(5000),TF(5000),NC(5000),IC(4,5000)
      CHARACTER*4 TITLE(15),RELIST(500),NAMAT(5000),NAMRE(5000)
      CHARACTER*8 PAN1(40000),PAN3(40000)
      DIMENSION   NPRN2(40000),NPRN4(40000)
      REAL*8 DSLW5(40000),DSUP5(40000),DIFF,DIS,DLOW,DUP,DSL,CUT
C
C     MODIFIED K.MORIKAMI
C
      DATA  NTLMIT/40000/
      DIMENSION LMARK(5000),X(3,4),ANG(10)
C
      CHARACTER*4 NAM,N1T(6),NMC1,NMC2
      CHARACTER*80 SPACE/' '/
C
      DATA N1T/'N   ','CA  ',4HC'  ,'NP  ','O   ' ,'CB  '/
      DATA NMC1,NMC2/'C   ' ,4HC'  /
      CHARACTER*4 IFORM,ISAME,IYES
      DATA IYES/'Y   '/,NTN/0/
C
C     MODIFIED BY K.MORIKAMI
      INTEGER EVERY
      INTEGER RESNS(10)
      CHARACTER*1 ENRICH
C
C ************** INPUT OF INITIAL PARAMETERS ********************
C     MODIFIED BY K.MORIKAMI
C
            WRITE(6,6900)
 6900 FORMAT(//' ****   ANSWER  FOLLOWING  QUETIONS *******'//
     1     ' 1) FORMAT  ? ; WITH => Y , WITHOUT => N ')
            READ(5,5900) IFORM
 5900 FORMAT(A4)
            WRITE(6,6901)
 6901 FORMAT(' 2) DSL = ?;(F-TYPE) VALUE OF LOWER LIMIT OR UPPER LIMIT')
            READ(5,*) DSL
C
C     MODIFIED BY K.MORIKAMI
C
            IF(DSL.EQ.0.0)THEN
              ISAME='Y   '
            ELSE
              ISAME='N   '
            ENDIF
            WRITE(6,6903)
 6903 FORMAT(' 3) CUT= ?, EVERY ? (F-TYPE)  ')
            READ(5,*) CUT,EVERY
            WRITE(6,6904)
 6904 FORMAT(' 4) LBPAIR = ? ; PAIR OF LONGRANGE BACBONE (F TYPE)')
            READ(5,*) LBPAIR
C
C     MODIFIED BY K.MORIKAMI 1986.12.08
C
      WRITE(6,6902)
 6902 FORMAT(' ','5) DO YOU ENRICH CONSTRAINTS ? (Y OR N) (A1)')
      READ(5,5902)ENRICH
 5902 FORMAT(A1)
      IF(ENRICH.EQ.'Y')THEN
        WRITE(6,7000)
 7000   FORMAT(' ','   NUMBER OF RESIDUES TO ENRICH CONSTRAINTS ?')
        READ(5,*)NRE
        DO 7001 I=1,NRE
          WRITE(6,7002)I
 7002     FORMAT(' ',I5,'   RESIDUE NUMBER ?')
          READ(5,*)RESNS(I)
 7001   CONTINUE
      ENDIF
C ***************** END OF INITIAL PARAMETERS *********************
C
C============= READ DATA FROM COORDINATE FILE (ABE FORMAT)=======
C        SUBROUTINE INPUT : READ DATA FROM STEP1 FILE
C
         CALL   INPUT(TITLE,LAST,NUATOM,NSS,NCYS,RELIST,NPAIR,NUM,
     1                NAMAT,NAMRE,NUMRE,OCUP,TF,NC,IC)
C
C
C============= READ SIDE CHAIN DATA FROM OVREG2.DATA ============
C        SUBROUTINE INPUT2 : READ DATA FROM OVREG2.DATA
C
      CALL INPUT2
C============= CHECK RESIDUES AND CONVERTION =====================
      CALL RSNANU(LAST,RELIST)
C
      WRITE(6,1000)TITLE,NUATOM
 1000 FORMAT(' STRUCTURE: ',15A4//' NUMBER OF ATOMS:',I5)
C
C
C================  CHANGE ATOMNAME FOR C   (C==>C')==============
C================  CHANGE ATOMNAME FOR PRO (N==>NP)==============
C     MODIFIED BY K.MORIKAMI
C
      DO 5 IND=1,NUATOM
        IF((NAMAT(IND).EQ.'N   ').AND.(NAMRE(IND).EQ.'PRO '))THEN
          NAMAT(IND)='NP  '
        ENDIF
        IF(NAMAT(IND).EQ.NMC1)NAMAT(IND)=NMC2
    5 CONTINUE
C
C CALCULATE DIHEDRAL ANGLES OF BACKBONE AND SIDE CHAIN
C
       LMAX=0
C
C      LMAX = NUMBER OF BACKBONE ATOMS (CA,C',N,NP)
C
C     PREPARATION FOR CALCULATION OF DIHEDRAL ANGLES
C       LMARK(I) : BACKBONE ATOM NUMBERS
C
      DO 35 IND=1,NUATOM
        NAM=NAMAT(IND)
        DO 36 K=1,4
          IF(NAM.NE.N1T(K))GOTO 36
          LMAX=LMAX+1
          LMARK(LMAX)=IND
  36    CONTINUE
  35  CONTINUE
C
      NPREA=0
C
C
C
COMMENT BY NOGUTI 12/'85
* TOP LINE IN OUTPUT FILE FOR DIHEDRAL ANGLES ARE FILLED WITH SPACE.
COMMENT END
C
C      WRITE N-END GROUP DIHEDRAL ANGLES TO DIHEDRAL ANGLES DATA FILE
C            DIHEDRAL ANGLES ARE ALL 0.0
C
       WRITE(23,6957) SPACE
 6957  FORMAT(A80)
C
C      CALCULATE FIRST AMINO ACID'S DIHEDRAL ANGLES AND WRITE THEM
C      TO DATA FILE
C
       DO 40 L=1,4
         DO 41 K=1,3
           X(K,L)=CO(K,LMARK(L))
  41     CONTINUE
  40   CONTINUE
C
C      SUBROUTINE DHD CALCULATE DIHEDRAL ANGLES
C
       CALL DHD(X,PSI)
C
C
      IND=1
      DO 42 L=1,4
        IND=IND+1
        DO 43 K=1,3
          X(K,L)=CO(K,LMARK(IND))
  43    CONTINUE
  42  CONTINUE
C
      CALL DHD(X,OMEGA)
C
C
      LIST2=LIST(2)
      CALL KAIANG(LIST2,NUMANG,ANG)
C
C     FIRST AMINO ACID'S PHI IS 180.0
C
      ANG(1)=180.0
      ANG(2)=PSI
      ANG(3)=OMEGA
      IF (LIST2.EQ.8) THEN
C
C     IF AMINO ACID IS ILE(NUMBER=8) , CHANGE DIHEDRAL ANGLE'S ORDER
C
            T=ANG(5)
            ANG(5)=ANG(6)
            ANG(6)=T
      ENDIF
C
C     NUMANG = NUMBER OF DIHEDRAL ANGLES
C
      WRITE(23,2000) (ANG(K),K=1,NUMANG)
C
C  LOOP OVER ALL RESIDUES BUT THE FIRST AND THE LAST
C
        IR=LMAX/3-2
C
      DO 50 IRES=1,IR
        LISTI=LIST(IRES+2)
        DO 48 IW=1,3
          IND=3*IRES+IW-2
          DO 45 L=1,4
            IND=IND+1
            DO 46 K=1,3
              X(K,L)=CO(K,LMARK(IND))
  46        CONTINUE
  45      CONTINUE
C
          CALL DHD(X,ANG(IW))
  48    CONTINUE
C
        CALL KAIANG(LISTI,NUMANG,ANG)
        IF (LISTI.EQ.8) THEN
          T=ANG(5)
          ANG(5)=ANG(6)
          ANG(6)=T
        ENDIF
C
        WRITE(23,2000) (ANG(K),K=1,NUMANG)
C
   50 CONTINUE
C
C LAST RESIDUE
C
      LASTFL=LIST(LAST-1)
      IND=LMAX-4
      DO 52 L=1,4
        IND=IND+1
        DO 53 K=1,3
          X(K,L)=CO(K,LMARK(IND))
  53    CONTINUE
  52  CONTINUE
C
      CALL DHD(X,PHI)
C
      CALL KAIANG(LASTFL,NUMANG,ANG)
      ANG(1)=PHI
C
C     LAST AMINO ACID'S PSI AND OMEGA ARE 180.0
C
      ANG(2)=180.0
      ANG(3)=180.0
      IF (LASTFL.EQ.8) THEN
            T=ANG(5)
            ANG(5)=ANG(6)
            ANG(6)=T
      ENDIF
      WRITE(23,2000) (ANG(K),K=1,NUMANG)
*MODIFICATION BY NOGUTI
 2000 FORMAT(10F8.3)
*MODIFICATION END
*INSERTION BY NOGUTI
C     WRITE C-END GROUP'S DIHEDRAL ANGLES TO DATA FILE
C           DIHEDRAL ANGLES ARE ALL 0.0
C
      WRITE(23,6957)SPACE
COMMENT BY NOGUTI
*      BOTTOM LINE IS FILLED WITH SPACE
COOMENT END
C============ DIHEDRAL ANGLES CALCULATION ENDED =====================
C
C
C============ CALCULATE DISTANCE CONSTRAINTS =========================
C
C     CALCULATE TYPE1 CONSTRAINTS
C               TYPE1 : BACKBONE DISTANCES
C                       EVERY LBPAIR ATOM NUMBERS
C                       FAR 4 ATOMS
C
C                       PERHAPS THESE CONSTRAINTS DETERMINE
C                       BACKBONE STRUCTURE
C
C         NTN = NUMBER OF TOTAL CONSTRAINTS
C         IT1 = NUMBER OF TYPE1 CONSTRAINTS
C         IT2 = NUMBER OF TYPE2 CONSTRAINTS
C         IT3 = NUMBER OF TYPE3 CONSTRAINTS
C         IT3A = NUMBER OF TYPE3(DISTANCE<=CUT) CONSTRAINTS
C         IT3B = NUMBER OF TYPE3(DISTANCE> CUT) CONSTRAINTS
C         IT4 = NUMBER OF TYPE4 CONSTRAINTS
C
          IT1=0
C
      DO 720 I=1,LMAX-LBPAIR
        IND=LMARK(I)
        DO 725 J=I+4,LMAX,LBPAIR
          JND=LMARK(J)
          DIS=0
          DO 730  K=1,3
            DIFF=CO(K,IND)-CO(K,JND)
            DIS=DIS+DIFF*DIFF
  730     CONTINUE
          DIS=SQRT(DIS)
          NTN=NTN+1
          IT1=IT1+1
          IF(IFORM.NE.IYES) GO TO  1755
          WRITE(21,5001)NAMAT(IND),NUMRE(IND),NAMAT(JND),NUMRE(JND),DIS
          IF(ISAME.EQ.IYES) GO TO 725
          WRITE(22,5001)NAMAT(IND),NUMRE(IND),NAMAT(JND),NUMRE(JND),DIS
          GO TO 725
C  -------- WHEN   WRITE WITHOUT FORMAT ------------------------
 1755     PAN1(NTN)=NAMAT(IND)
          NPRN2(NTN)=NUMRE(IND)
          PAN3(NTN)=NAMAT(JND)
          NPRN4(NTN)=NUMRE(JND)
          DSLW5(NTN)=DIS
          DSUP5(NTN)=DIS
  725   CONTINUE
  720 CONTINUE
C
C     CALCULATE TYPE2 CONSTRAINTS
C               TYPE2 : 1-4 DISTANCES
C                       SHORT RANGE CONSTRAINTS
C                       PERHAPS THESE CONSTRAINTS DETERMINE
C                       LOCAL STRUCTURE SUCH AS AMINO ACID'S
C                       STRUCTURE
C
C
      IT2=0
      DO 110 IND=1,NUATOM
        DO 120 JB=1,NC(IND)
          IBEG=IC(JB,IND)
          IF(NC(IBEG).LE.1)GOTO 120
C
            DO 130 JE=1,NC(IBEG)
              IEND=IC(JE,IBEG)
              IF(IEND.LT.IBEG)GOTO 130
              IF(IEND.EQ.IND)GOTO 130
              IF(NC(IEND).LE.1)GOTO 130
C
              DO 140 JN=1,NC(IEND)
                JND=IC(JN,IEND)
                IF(JND.LE.IEND)GOTO 140
C------ OMITED BY IRISA FOR OMEGA VARIABLE VERSION. ('88 7/25)
C       THIS BUG IS POINTED OUT BY DR. SENOU
CC              IF(NAMAT(JND).EQ.N1T(2)) THEN
CC                IF(NAMAT(IND).EQ.N1T(2))GOTO 140
CC                IF(NAMAT(IND).EQ.N1T(5))GOTO 140
CC              ENDIF
C------ OMISSION END
                DIS=0.0
                DO 150  K=1,3
                  DIFF=CO(K,IND)-CO(K,JND)
                  DIS=DIS+DIFF*DIFF
150             CONTINUE
                DIS=SQRT(DIS)
                DLOW=DIS
                DUP=DIS
                NTN=NTN+1
                IT2=IT2+1
                IF(IFORM.NE.IYES) GO TO  2755
       WRITE(21,5002)NAMAT(IND),NUMRE(IND),NAMAT(JND),NUMRE(JND),DLOW
                IF(ISAME.EQ.IYES) GO TO 140
       WRITE(22,5002)NAMAT(IND),NUMRE(IND),NAMAT(JND),NUMRE(JND),DUP
                GO TO 140
C ------------- WHEN WRITE WITHOUT FORMAT --------------------
 2755           PAN1(NTN)=NAMAT(IND)
                NPRN2(NTN)=NUMRE(IND)
                PAN3(NTN)=NAMAT(JND)
                NPRN4(NTN)=NUMRE(JND)
                DSLW5(NTN)=DLOW
                DSUP5(NTN)=DUP
 5001         FORMAT(A3,I3,1X,A3,I3,F10.4,5X,'TYPE-1')
 5002         FORMAT(A3,I3,1X,A3,I3,F10.4,5X,'TYPE-2')
 5003         FORMAT(A3,I3,1X,A3,I3,F10.4,5X,'TYPE-3')
 5004         FORMAT(A3,I3,1X,A3,I3,F10.4,5X,'TYPE-4')
 140        CONTINUE
 130      CONTINUE
 120    CONTINUE
 110  CONTINUE
C
           IT3=0
           IT3A=0
           IT3B=0
C
C     CALCULATE TYPE3 CONSTRAINTS
C               TYPE3 : ALL ATOMS CONSTRAINTS
C                       HOPE DEFINING SIDE CHAIN
C
C             DISTANCE <= CUT
C                  DIFFERENCE OF TWO RESIDUE NUMBERS >= 2
C             DISTANCE >  CUT
C                  CA OF EVERY 'EVERY' RESIDUES
C                  EXCEPT SAME RESIDUE
C
C     MODIFIED BY K.MORIKAMI 1986.11.26
C
      DO 400 NA=1,NUATOM-1
        DO 405 NA2=NUATOM,NA+1,-1
          DIS=0.0
          IDNRE=IABS(NUMRE(NA)-NUMRE(NA2))
          DO 401 K=1,3
            DIFF=CO(K,NA)-CO(K,NA2)
            DIS=DIS+DIFF**2
 401      CONTINUE
          DIS=SQRT(DIS)
          IF(DIS.LE.CUT)THEN
            IF(IDNRE.LE.1)GO TO 405
              IT3A=IT3A+1
          ELSE
           IF(MOD(IDNRE,EVERY).NE.0)GO TO 405
           IF(IDNRE.EQ.0)GO TO 405
           IF((NAMAT(NA).NE.'CA  ').AND.(NAMAT(NA2).NE.'CA  '))GO TO 405
             IT3B=IT3B+1
          ENDIF
C
C
           DLOW=DIS-DSL
           DUP=DIS+DSL
          NTN=NTN+1
          IT3=IT3+1
C  ========= CHACK OF NTLMIT ========================
            IF(NTN.GT.NTLMIT) GO TO 9998
           IF(IFORM.NE.IYES) GO TO  3755
       WRITE(21,5003)NAMAT(NA),NUMRE(NA),NAMAT(NA2),NUMRE(NA2),DLOW
           IF(ISAME.EQ.IYES) GO TO 405
       WRITE(22,5003)NAMAT(NA),NUMRE(NA),NAMAT(NA2),NUMRE(NA2),DUP
                GO TO 405
C ------------- WHEN WRITE WITHOUT FORMAT --------------------
 3755     PAN1(NTN)=NAMAT(NA)
          NPRN2(NTN)=NUMRE(NA)
          PAN3(NTN)=NAMAT(NA2)
          NPRN4(NTN)=NUMRE(NA2)
          DSLW5(NTN)=DLOW
          DSUP5(NTN)=DUP
 405    CONTINUE
  400 CONTINUE
C
C     CALCULATE TYPE4 CONSTRAINTS
C               TYPE4 : ALL ATOMS CONSTRAINTSOF RESIDUES TO BE DEFINED
C                       NON USUSAL USE
C                       HOPE TO DELETE PEAKS OF CONFFIT
C
C     MODIFIED BY K.MORIKAMI 1986.12.08
C
      IF(ENRICH.EQ.'Y')THEN
        DO 500 I=1,NUATOM
          DO 501 J=1,NRE
            IF(NUMRE(I).EQ.RESNS(J))THEN
              DO 502 K=1,NUATOM
                IDNRE=IABS(NUMRE(I)-NUMRE(K))
                IF(IDNRE.LE.1)GO TO 502
                NAM=NAMAT(K)
                DO 503 L=1,4
                  IF(NAM.NE.N1T(L))GO TO 503
                    DIS=0.0
                    DO 504 M=1,3
                      DIFF=CO(M,I)-CO(M,K)
                      DIS=DIS+DIFF**2
  504               CONTINUE
                  DIS=SQRT(DIS)
                  IT4=IT4+1
                  NTN=NTN+1
C  ========= CHACK OF NTLMIT ========================
            IF(NTN.GT.NTLMIT) GO TO 9998
           IF(IFORM.NE.IYES) GO TO  4755
       WRITE(21,5004)NAMAT(I),NUMRE(I),NAMAT(K),NUMRE(K),DIS
           IF(ISAME.EQ.IYES) GO TO 502
       WRITE(22,5004)NAMAT(I),NUMRE(I),NAMAT(K),NUMRE(K),DIS
                GO TO 502
C ------------- WHEN WRITE WITHOUT FORMAT --------------------
 4755     PAN1(NTN)=NAMAT(I)
          NPRN2(NTN)=NUMRE(I)
          PAN3(NTN)=NAMAT(K)
          NPRN4(NTN)=NUMRE(K)
          DSLW5(NTN)=DIS
          DSUP5(NTN)=DIS
  503           CONTINUE
  502         CONTINUE
            ENDIF
  501     CONTINUE
  500   CONTINUE
      ENDIF
C
C ========= END OF CALCULATION CONSTRAINTS =============
C
      WRITE(6,6259) IT1,IT2
      WRITE(6,6998)IT3,IT4
      WRITE(6,6999)IT3A,IT3B
      WRITE(6,6260) NTN
 6259 FORMAT(/' ** NO. OF PAIRS OF TYPE-1, TYPE-2=',2I10)
 6998 FORMAT(/' ** NO. OF PAIRS OF TYPE-3, TYPE-4=',2I10)
 6999 FORMAT(/' ** NO. OF PAIRS OF TYPE-3A , TYPE-3B= ',2I10)
 6260 FORMAT(/' **TOTAL NO. OF PAIRS OF ATOMA IN DIS. CONSTRAINTS=',I10)
            IF(IFORM.EQ.IYES) GO TO 9999
C ******  OUTPUT OF VALUES OF DISTANCE CONSTRAINTS WITHOUT FORMAT*****
      WRITE(51) NTN,( PAN1(I),NPRN2(I), PAN3(I),NPRN4(I),
     1                DSLW5(I),DSUP5(I),I=1,NTN)
C ------------------------------------------------------------------
 9999  CONTINUE
         WRITE(6,6250) IFORM,ISAME
 6250 FORMAT(//' IFORM=  ',A4,'  ISAME=  ',A4)
             GO TO 9979
 9998  WRITE(6,6398) NTN,NTLMIT
 6398  FORMAT('  DIMENSION IS SMALL ; NTN=',I9,'  NTLIMIT=',I9)
 9979  STOP
       END
C
C
C
      SUBROUTINE INPUT(TITLE,LAST,NUATOM,NSS,NCYS,RELIST,NPAIR,NUM,
     1 NAMAT,NAMRE,NUMRE,OCUP,TF,NC,IC)
C **************** INPUT READS DATA FROM PTI3C.DATA (UNIT=20) *******
C
      COMMON /COORD/  CO(3,5000)
C
      DIMENSION NPAIR(20,2),NUM(5000),NUMRE(5000),
     1        OCUP(5000),TF(5000),NC(5000),IC(4,5000)
      CHARACTER*4 TITLE(15),RELIST(500),NAMAT(5000),NAMRE(5000)
      DATA MD/20/
C ********** INPUT FROM DISK ****************************************
           READ(MD,1610) (TITLE(I),I=1,15)
           READ(MD,1620) LAST,NUATOM,NSS,NCYS
           READ(MD,1630) (RELIST(I),I=1,LAST)
              IF(NSS)  6641,6645,6645
 6641      READ(MD,1640) ((NPAIR(I,J),J=1,2),I=1,NCYS)
 6645    CONTINUE
               DO  6665 J=1,NUATOM
           READ(MD,1650) NUM(J),NAMAT(J),NAMRE(J),NUMRE(J),
     *   (CO(K,J),K=1,3),OCUP(J),TF(J),NC(J),(IC(K,J),K=1,4)
 6665      CONTINUE
C ********** DEBUG OF INPUT ****************************************
         MD=6
          WRITE(MD,1610) (TITLE(I),I=1,15)
          WRITE(MD,1620) LAST,NUATOM,NSS,NCYS
          WRITE(MD,1630) (RELIST(I),I=1,LAST)
              IF(NSS)  7641,7645,7645
 7641     WRITE(MD,1640) ((NPAIR(I,J),J=1,2),I=1,NCYS)
 7645    CONTINUE
               DO  7665 J=1,20
          WRITE(MD,1650) NUM(J),NAMAT(J),NAMRE(J),NUMRE(J),
     *   (CO(K,J),K=1,3),OCUP(J),TF(J),NC(J),(IC(K,J),K=1,4)
 7665      CONTINUE
 1610  FORMAT(5X,15A4)
 1620  FORMAT(4I5)
 1630  FORMAT(5X,10A4)
 1640  FORMAT(20I4)
 1650  FORMAT(I5,1X,A4,A4,I4,3F8.3,2F6.2,I2,4I5)
      RETURN
      END
C...
C...
C...
      SUBROUTINE INPUT2
C
C     THIS SUBROUTINE READS SIDE CHAIN DATA FROM OVREG2.DATA
C
      COMMON /SCHDAT/ NHEAVY(21),NCHI(21),NCHANG(4,7,21)
      CHARACTER*80 DUMMY
      DO 10 I=1,21
        READ(24,500) DUMMY
        READ(24,501) NHEAVY(I),NCHI(I)
        NC=NCHI(I)
        DO 11 J=1,NC
          READ(24,502) (NCHANG(K,J,I),K=1,4)
   11   CONTINUE
   10 CONTINUE
      RETURN
  500 FORMAT(A80)
  501 FORMAT(2I5)
  502 FORMAT(4I5)
      END
C...
C...
C...
C SUBROUTINE DHD TO CALCULATE DIHEDRALANGLES
      SUBROUTINE DHD(X,ANG)
      DIMENSION X(3,4)
      DIMENSION E1(3),E2(3),E3(3),U(3),V(3)
C
      DATA PI/3.141593/
        NLOW=0
      DO 10 K=1,3
        E1(K)=X(K,1)-X(K,2)
        E2(K)=X(K,3)-X(K,2)
        E3(K)=X(K,4)-X(K,3)
 10   CONTINUE
      CALL DOT(E1,E1,DS1)
      CALL DOT(E2,E2,DS2)
      CALL DOT(E3,E3,DS3)
        DS1=SQRT(DS1)
        DS2=SQRT(DS2)
        DS3=SQRT(DS3)
      DO 30 K=1,3
       E1(K)=E1(K)/DS1
       E2(K)=E2(K)/DS2
       E3(K)=E3(K)/DS3
 30    CONTINUE
C
       CALL DOT(E1,E2,S12)
       CALL DOT(E2,E3,S23)
      DO 50 K=1,3
        U(K)=E1(K)-S12*E2(K)
        V(K)=E3(K)-S23*E2(K)
  50   CONTINUE
       CALL DOT(U,U,UN)
       CALL DOT(V,V,VN)
       UN=SQRT(UN)
       VN=SQRT(VN)
       DO 70 K=1,3
       U(K)=U(K)/UN
       V(K)=V(K)/VN
  70   CONTINUE
       CALL DOT(U,V,S)
C
C DETERMINE SIGN
       CALL CROSS(U,E2,E1)
       CALL DOT(E1,V,DET)
       SIGN=-1.0
       IF(DET.LT.0.0)SIGN=1.0
       ANG=SIGN*ACOS(S)
       ANG=(ANG*180.)/PI
       RETURN
       END
C
C SUBROUTINE DOT TO CALCULATE SCALARPRODUCT
C
      SUBROUTINE DOT(X,Y,S)
      DIMENSION X(3),Y(3)
      S=0.
      DO 10 K=1,3
10    S=S+X(K)*Y(K)
      RETURN
      END
C
C SUBROUTINE CROSS TO CALCULATE THE CROSSPRODUCT
C
      SUBROUTINE CROSS(X,Y,Z)
      DIMENSION X(3),Y(3),Z(3)
      Z(1)=X(2)*Y(3)-X(3)*Y(2)
      Z(2)=X(3)*Y(1)-X(1)*Y(3)
      Z(3)=X(1)*Y(2)-X(2)*Y(1)
      RETURN
      END
C
C...
C...
C...
      SUBROUTINE RSNANU(INUMRS,NAMRE)
      COMMON /LISTRS/ NPREA,LIST(500)
      CHARACTER*4 NAMEND(20),NAMFUL(26),NAMRE(500)
C
C
      DATA NAMEND/'H2N ','H3N ','CH3N','MCON','HCON',
     1           'CISH','TRAH','H2NP','P-GL','HN  ',
     2           'COOH','COO ','COM ','CONH','CONM',
     3           'COM2','COOM','COOE','CO  ','CO  '/

C
      DATA NAMFUL/'ALA ','ASP ','CYS ','GLU ','PHE ',
     1           'GLY ','HIS ','ILE ','LYS ','LEU ',
     2           'MET ','ASN ','PRO ','GLN ','ARG ',
     3           'SER ','THR ','VAL ','TRP ','TYR ',
     4           'CYE ','H-PR','NORL','ORN ','HISE','BEAS'/
C
      DO 10 L=1,10
      IF(NAMRE(1).NE.NAMEND(L))GOTO 10
      LIST(1)=L
      GOTO 15
 10   CONTINUE
C
      STOP' NO VALID AMINO ENDGROUP'
C
 15   CONTINUE
      LAST=INUMRS-1
      DO 20 N=2,LAST
C
        DO 30 L=1,26
        IF(NAMRE(N).NE.NAMFUL(L))GOTO 30
        LIST(N)=L
        GOTO 20
 30   CONTINUE
      WRITE(6,2000)NAMRE(N)
 2000 FORMAT(A4,' IS NOT A VALID RESIDUE')
      STOP
C
C
 20   CONTINUE
C
      DO 40 L=11,20
C
      IF(NAMRE(INUMRS).NE.NAMEND(L))GOTO 40
      LIST(INUMRS)=L
      GOTO 45
C
 40    CONTINUE
C
      STOP' NO VALID CARBOXYL ENDGROUP'
C
 45   CONTINUE
      RETURN
      END
C...
C...
C...
      SUBROUTINE KAIANG(LISTI,KEND,ANG)
      COMMON /LISTRS/ NPREA,LIST(500)
      COMMON /SCHDAT/ NHEAVY(21),NCHI(21),NCHANG(4,7,21)
      COMMON /COORD/  CO(3,5000)
      DIMENSION X(3,4),ANG(10)
      NC=NCHI(LISTI)
      IF (NC.EQ.0) GO TO 1000
      DO 10 J=1,NC
      IF (NCHANG(1,J,LISTI).NE.0) THEN
           DO 20 L=1,4
           LN=NPREA+NCHANG(L,J,LISTI)
           DO 21 K=1,3
   21      X(K,L)=CO(K,LN)
   20      CONTINUE
           CALL DHD(X,AKAI)
           ANG(J+3)=AKAI
      ELSE
           ANG(J+3)=180.0
      ENDIF
   10 CONTINUE
 1000 KEND=NC+3
      NPREA=NPREA+NHEAVY(LISTI)
      RETURN
      END
